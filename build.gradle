plugins {
    id "com.gradle.plugin-publish" version "1.3.1"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'
apply plugin: 'codenarc'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.spockframework:spock-core:2.3-groovy-4.0') {
        exclude(module: 'groovy-all')
    }
    testImplementation('org.spockframework:spock-junit4:2.3-groovy-4.0')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.1'
    testImplementation 'junit:junit:4.13.1'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

group = 'se.patrikerdes'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["STARTED", "PASSED", "FAILED", "SKIPPED", "STANDARD_ERROR", "STANDARD_OUT"]
    }
}

codenarcMain {
    configFile = rootProject.file("config/codenarc/main_rules.groovy")
}

codenarcTest {
    configFile = rootProject.file("config/codenarc/test_rules.groovy")
}

codenarc {
    toolVersion = "3.6.0"
}

gradlePlugin  {
    website = 'https://github.com/patrikerdes/gradle-use-latest-versions-plugin/blob/master/README.md'
    vcsUrl = 'https://github.com/patrikerdes/gradle-use-latest-versions-plugin'
    description = 'A plugin that updates the versions of your dependencies in your gradle project to the ' +
            'latest available versions.'
    plugins {
        create('useLatestVersionsPlugin') {
            id = 'se.patrikerdes.use-latest-versions'
            implementationClass = 'se.patrikerdes.UseLatestVersionsPlugin'
            displayName = 'Use Latest Versions'
            description = 'A plugin that updates the versions of your dependencies in your *.gradle files to the ' +
                    'latest available versions. This plugin depends on the Gradle Versions Plugin.'
            tags = ['version','versions','dependency','dependencies','update','updates','upgrade','upgrades']
        }
    }
}
